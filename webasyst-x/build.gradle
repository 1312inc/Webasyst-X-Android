apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'idea'

def webasystProperties = new Properties()
webasystProperties.load(new FileInputStream(rootProject.file('webasyst.properties')))

buildscript {
    ext.nav_version = "2.3.0"
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
    }
}

android {
    compileSdkVersion target_sdk_version
    buildToolsVersion build_tools_version

    lintOptions {
        abortOnError = false
        checkReleaseBuilds = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        languageVersion = "1.4"
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding true
    }

    defaultConfig {
        applicationId 'com.webasyst.x'
        minSdkVersion 16
        targetSdkVersion target_sdk_version
        def versionMajor = 0
        def versionMinor = 0
        def versionPatch = 8
        versionCode versionMajor * 1000000 + versionMinor * 1000 + versionPatch
        versionName "$versionMajor.$versionMinor.$versionPatch"
        manifestPlaceholders = ['appAuthRedirectScheme': 'YOURSCHEME']

        vectorDrawables.useSupportLibrary = true

        buildConfigField('String', 'CLIENT_ID', webasystProperties['webasyst.x.client_id'])
        buildConfigField('String', 'WEBASYST_HOST', webasystProperties['webasyst.x.host'])
    }

    signingConfigs {
        release {
            storeFile file('keystore.jks')
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias 'webasyst_x_release'
            keyPassword System.getenv('WEBASYST_X_RELEASE_KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation project(':api')
    implementation project(':auth:kt')
    implementation project(':api:waid')
    implementation project(':api:webasyst')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.core:core-ktx:1.3.1"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    def lifecycle_version = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    def ktor_version='1.4.0'
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-client-gson:$ktor_version"
}
